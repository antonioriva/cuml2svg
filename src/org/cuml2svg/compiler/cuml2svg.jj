/**
 * JavaCC file
 */


options {
  JDK_VERSION = "1.5";
  LOOKAHEAD=2;
  STATIC=false;
}
PARSER_BEGIN(cuml2svg)
package org.cuml2svg.compiler;

import java.io.FileInputStream;
import java.io.FileNotFoundException;
public class cuml2svg {
  public static void main(String args[]) throws ParseException {
      try {
    	 	cuml2svg parser = new cuml2svg(new FileInputStream("doc/sorgenti_test/visitor.u2sl"));
    		parser.s();
        
      } catch (FileNotFoundException e1) {
    		e1.printStackTrace();
    }catch (Exception e) {
        System.out.println("NOK.");
        System.out.println(e.getMessage());
      } 
      catch (Error e) {
        System.out.println("Oops.");
        System.out.println(e.getMessage());
      }
  
}
}
PARSER_END(cuml2svg)

SKIP : {
	< ("//" (~["\n"])* ) >
//|	< ("/*" (~[])*  "*/") >
| 	" "
|	"\r"
|	"\t"
|	"\n"

}

MORE : { "/*": IN_COMMENT } 
< IN_COMMENT > MORE : {<  ~[] > } 
< IN_COMMENT > SKIP : {  "*/": DEFAULT }

TOKEN:{
//	<COMMENT: "//"(~["\n"])* | "#"(~["\n"])* |	"/*"(~[])*"*/" >

 	<COMMENT: ("#"(~["\n"])*)>
|	<PACKAGE: "package">
|	<RELATIONS: "relations">
|	<ATTRIBUTES: "attributes">	
|	<METHOD: "methods" >
//|	<IMPORT: "import">
}

TOKEN:{
	<IMPORT: "import"> : IMPORT_STATE
}
<IMPORT_STATE>  TOKEN : { <FILE_NAME: (~[";"])* > : DEFAULT  }


TOKEN:{
	<LAYOUT: "@layout"> : LAYOUT_STATE
} 
<LAYOUT_STATE> SKIP:{	< ("//" (~["\n"])* ) >
| 	" "
|	"\r"
|	"\t"
|	"\n"}
<LAYOUT_STATE>  TOKEN : { <LAYOUT_CARD: ((["1"-"9"])(["0"-"9"])* "x*") | ("*x" (["1"-"9"])(["0"-"9"])*) | ("*x*") > : DEFAULT  }
<LAYOUT_STATE>  TOKEN : { <OVERLAPPED: "overlapped"> : DEFAULT}




TOKEN:{
	<VISIBILITY: "private" | "public" | "protected">
| 	<RELATION_TYPE: "use" | "extend" |"associate"| "include"| "composed"| "realize"| "depend" >
|	<CLASS_TYPE: "interface" | "class">
|	<EQUAL:"=">
}

TOKEN:{
	<STYLE: "@style">
|	<POSITION: "@position">
|	<GROUP: "@group">
|	<BACKGROUND_COLOR: "@backgroud-color">
|	<COLOR: "@color">


}

TOKEN:{
	< SIZE:(["0"-"9"])+ (("cm")|("px"))? >	
|  	<NUMBER:  ("+" | "-")? (["0" - "9"])+ ("." (["0" - "9"] )+)?>
|	<STRING: "\"" (~[])* "\"">
|	<VARIABLE: (["a"-"z" , "A"-"Z" ])( ["0" - "9", "a"-"z" , "A" - "Z" , "_"] )+>
|	<HEX_COLOR: ("#" (["0"-"9", "a"-"f", "A"-"F"]){3} (["0"-"9", "a"-"f", "A"-"F"]){3})>
|	<CLASS_VARIABLE: (["a"-"z" , "A"-"Z" ])( ["0" - "9", "a"-"z" , "A" - "Z" , "_"] )+ ("."(["a"-"z" , "A"-"Z" ])( ["0" - "9", "a"-"z" , "A" - "Z" , "_"] )+)*>
}








////////////////////////////////////////////////////////////////
//S → diagram DIAGRAM_TYPE diagram_name { DIAGRAM_CONTENT }
void s() :  {}
{
//	model()
	{System.out.println("Starting parsing");}
	layout()
}

void layout():{}{
	(import_definition())+	
	groups()
}

void import_definition():{
	Token n;
}{
	<IMPORT> 
	n = <FILE_NAME> {
		try{
			System.out.println("Importing model file: " + n.image);
			cuml2svg modelParser = new cuml2svg(new FileInputStream(n.image.trim()));
    		modelParser.model();
    	} catch (FileNotFoundException e1) {
    		e1.printStackTrace();
    	}
	}
	";"
	
	
}

void main_group():{}{
	"[" groups() "]"
}

void groups():{}{
	"[" ((group_preference())* 
		(groups()	 | group_definition()	)+) "]"
}

void group_preference():{}{
	
	layout_preference()
|	position_preference()
//|	group_preference()
|	color_preference()


}

void group_definition():{}{
	"[" <CLASS_TYPE> (<VARIABLE>|<CLASS_VARIABLE>) "]"

}

void layout_preference():{}{
	
	<LAYOUT>  <LAYOUT_CARD> 
|	<LAYOUT> <OVERLAPPED> 
}

void position_preference():{}{
	<POSITION> coordinate() coordinate()
}





void coordinate():{}{
	<SIZE>

}

void color_preference():{}{
	<COLOR> <HEX_COLOR>
|	<BACKGROUND_COLOR> <HEX_COLOR>
}
	



////////////////////////////////////////////////////////////////
//DIAGRAM_CONTENT → PACKAGE_DEFINITIONS
//DIAGRAM_CONTENT → CLASS_DEFINITIONS
//enable to skip package definition for 1 package diagram
//force the diagram to have at leas one package or one classes
void model() :  {}
{
	(class_definition())+ 
|	(package_definition())+
}




///////////////////////////////////////////////////////////////////
//PACKAGE_DEFINITION → package package_name { CLASS_DEFINITIONS }
void package_definition():{}{
	<PACKAGE> package_name() "{" (class_definition())+ "}"
}
void package_name():{}{
	<VARIABLE>
}


///////////////////
//COMMENT → comment
void comment():{}{
	<COMMENT>
}



///////////////////////////////////////////////////////////////////////////////////////////////////////
//CLASS_DEFINITION → OPTIONAL_COMMENT VISIBILITY CLASS_TYPE class_name { RELATIONS ATTRIBUTES METHODS }
void class_definition():{}{
	(comment())? (visibility())? <CLASS_TYPE> class_name() "{" (relations())? (attributes())? (methods())? "}"
}
void class_name():{}{
	<VARIABLE>
}



/////////////////////////////////////////////////
//RELATIONS → relations { RELATION_DEFINITIONS }
void relations():{}{
	<RELATIONS> "{" (relation())+ "}"
}




////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//RELATION_DEFINITION → OPTIONAL_COMMENT RELATION_TYPE class_name RELATION_CARDINALITY CLASS_LIST_WITH_CARDINALITY
void relation():{}{
	(comment())? <RELATION_TYPE> class_name() (relation_cardinality())? ("," class_name() (relation_cardinality())? )* ";"
}




////////////////////////////////////////////////////////////////
//RELATION_CARDINALITY → ε
//RELATION_CARDINALITY → (CARDINALITY, CARDINALITY, CARDINALITY)
void relation_cardinality():{}{
	"(" (cardinality())? "," (cardinality())? "," (cardinality())? ")"
}

//////////////////////
//CARDINALITY → string
//CARDINALITY → ε
void cardinality():{}{
	<STRING>
}

////////////////////////////////////
//ATTRIBUTES → ATTRIBUTE_DEFINITIONS
//ATTRIBUTES → ε
void attributes():{}{
	<ATTRIBUTES> "{" (attribute())+ "}"
}



////////////////////////////////////////////////////////////////////////////
//ATTRIBUTE → COMMENT VISIBILITY ATTRIBUTE_TYPE attribute_name DEFAULT_VALUE
void attribute():{}{
	(comment())? 
	(visibility())? 
	(typed_attribute_name() | attribute_name() )
	(default_value())? ";"
}
void attribute_name():{}{
	<VARIABLE>
}

void typed_attribute_name():{}{
	<VARIABLE> <VARIABLE>
}

/////////////////////////
//DEFAULT_VALUE → ε
//DEFAULT_VALUE → EQUAL_TO
void default_value():{}{	
	<EQUAL> equal_to()
}




/////////////////////////
//EQUAL_TO →  value
void equal_to():{}{
	<NUMBER>
| 	<STRING>
}



//////////////////////////
//METHODS → METHOD_DEFINITION
//METHODS → ε
void methods():{}{
	<METHOD> "{" (method())+ "}" 
}




/////////////////////////////////////////////////////////////////////
//METHOD → OPTIONAL_COMMENT VISIBILITY METHOD_TYPE method_name { METHOD_ARGS }
//METHOD_TYPE → TYPE
void method():{}{
	(comment())? 
	(visibility())? 
	(typed_method() | method_name())
	"(" (method_arg() ("," method_arg())* )? ")" ";"
}

void typed_method():{}{
	<VARIABLE> <VARIABLE>
}

void method_name():{}{
	<VARIABLE> 
}

//////////////////////////////////////////////////////
//METHOD_ARG → COMMENT ARG_TYPE arg_name DEFAULT_VALUE
//ARG_TYPE → TYPE
void method_arg():{}{
	(comment())? 
	(typed_arg() | arg_name())
	(default_value())?
}
void arg_name():{}{
	<VARIABLE>
}

void typed_arg():{}{
	<VARIABLE> <VARIABLE>
}



///////////////////////
//ATTRIBUTE_TYPE → TYPE
void attribute_type():{}{
	type()
}


///////////////
//TYPE → string
//TYPE → ε 
void type ():{}
{
	<VARIABLE> 
}

//////////////////////
//VISIBILITY → private
//VISIBILITY → public
//VISIBILITY → string
//VISIBILITY → ε 
void visibility ():{}
{
	<VISIBILITY>
}




